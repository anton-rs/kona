set positional-arguments
alias asterisc := build-client-prestate-asterisc-artifacts

# default recipe to display help information
default:
  @just --list

# Build the `kona-client` prestate artifacts for the latest release.
build-client-prestate-asterisc-artifacts kona_tag asterisc_tag out='./prestate-artifacts-asterisc':
  #!/bin/bash
  OUTPUT_DIR={{out}}

  # Docker bake env
  export GIT_REF_NAME="{{kona_tag}}"
  export DEFAULT_TAG="kona-asterisc-prestate:local"

  # Navigate to workspace root
  cd ../..

  # Create the output directory
  mkdir -p $OUTPUT_DIR

  echo "Building kona-client prestate artifacts for the asterisc target. ğŸš Kona Tag: {{kona_tag}} | ğŸ‡ Asterisc Tag: {{asterisc_tag}}"
  docker buildx bake \
    --set "*.output=$OUTPUT_DIR" \
    -f docker/docker-bake.hcl \
    kona-asterisc-prestate
